# CMake cross-platform build system
# 2009-2010 Ryan Pavlik <rpavlik@iastate.edu>
# http://academic.cleardefinition.com/
# Iowa State University HCI Graduate Program/VRAC
#
# Will set:
#  GLOVETOOLS_LIBRARIES
#  GLOVETOOLS_INCLUDE_DIRS
#  GLOVETOOLS_RUNTIME_LIBRARIES and GLOVETOOLS_RUNTIME_LIBRARY_DIRS if applicable
#  GLOVETOOLS_MODEL
#  GLOVETOOLS_FOUND


get_filename_component(_IMPORT_PREFIX "${CMAKE_CURRENT_LIST_FILE}" PATH)
include("${_IMPORT_PREFIX}/glovetools-targets.cmake")

set(GLOVETOOLS_LIBRARIES glovetools_imported_glovetools)
set(GLOVETOOLS_RUNTIME_LIBRARIES)
set(GLOVETOOLS_RUNTIME_LIBRARY_DIRS)


# Runtime library info - only useful for shared libraries
get_property(_type TARGET ${GLOVETOOLS_LIBRARIES} PROPERTY TYPE)
if(_type STREQUAL "SHARED_LIBRARY")
	get_property(_configs TARGET ${GLOVETOOLS_LIBRARIES} PROPERTY IMPORTED_CONFIGURATIONS)
	foreach(_config ${_configs})
		get_property(_libloc TARGET ${GLOVETOOLS_LIBRARIES} PROPERTY IMPORTED_LOCATION_${_config})
		if(EXISTS "${_libloc}")
			list(APPEND GLOVETOOLS_RUNTIME_LIBRARIES "${_libloc}")

			get_filename_component(_libpath "${_libloc}" PATH)
			list(APPEND GLOVETOOLS_RUNTIME_LIBRARY_DIRS ${_libpath})
		endif()
	endforeach()
endif()

if(GLOVETOOLS_RUNTIME_LIBRARIES)
	list(REMOVE_DUPLICATES GLOVETOOLS_RUNTIME_LIBRARIES)
endif()

if(GLOVETOOLS_RUNTIME_LIBRARY_DIRS)
	list(REMOVE_DUPLICATES GLOVETOOLS_RUNTIME_LIBRARY_DIRS)
endif()

# Compute the installation prefix relative to this file.
get_filename_component(_IMPORT_PREFIX "${CMAKE_CURRENT_LIST_FILE}" PATH)
get_filename_component(_IMPORT_PREFIX "${_IMPORT_PREFIX}" PATH)
if(NOT WIN32)
	get_filename_component(_IMPORT_PREFIX "${_IMPORT_PREFIX}" PATH)
endif()

set(GLOVETOOLS_INCLUDE_DIRS "${_IMPORT_PREFIX}/include")
set(GLOVETOOLS_MODEL "${_IMPORT_PREFIX}/@DATA_DIR@/hand-structured.osg")

set(_IMPORT_PREFIX)

set(GLOVETOOLS_FOUND ON)
if(NOT "${OPENSCENEGRAPH_VERSION}" VERSION_EQUAL "@OPENSCENEGRAPH_VERSION@")
	message(STATUS "glove-tools found, but current build uses OpenSceneGraph version ${OPENSCENEGRAPH_VERSION} while glove-tools was built with @OPENSCENEGRAPH_VERSION@!")
	set(GLOVETOOLS_FOUND OFF)
endif()
